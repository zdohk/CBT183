/*---------------------------------------------------------------REXX-*\
|*                                                                    *|
|* MODULE NAME = TALLY                                                *|
|*                                                                    *|
|* DESCRIPTIVE NAME = Tallies tracks on DSLIST panel.                 *|
|*                                                                    *|
|* STATUS = R102                                                      *|
|*                                                                    *|
|* FUNCTION = Display the total number of tracks used up by the       *|
|*            data sets currently displayed on the Data Set List      *|
|*            panel (ISPF 3.4).  Requires that the size information   *|
|*            for all the data sets be previously obtained from the   *|
|*            VTOC, for example with a SORT SIZE command.             *|
|*                                                                    *|
|* AUTHOR   =  Gilbert Saint-Flour <carlos@gsf-soft.com>              *|
|*             http://gsf-soft.com/Freeware/                          *|
|*                                                                    *|
|* DEPENDENCIES = TSO/E V2                                            *|
|*                ISPF V3, V4 or V5                                   *|
|*                                                                    *|
|* SYNTAX   =  TALLY                                                  *|
|*                                                                    *|
|*             Must be issued while on the DSLIST panel, either       *|
|*             as a line command (on any line) or as TSO %TALLY.      *|
|*                                                                    *|
|* CHANGE LOG                                                         *|
|*                                                                    *|
|*101 When DTA+016=0, get DSB addr from DTA+044                       *|
|*102 Stop using DTB in ISPF 5.9 and above                Ron.Hesketh *|
|*                                                                    *|
\*--------------------------------------------------------------------*/
ADDRESS 'ISPEXEC'
tcb    = PTR(540)               /* TCB (EXEC command)    PSATOLD  */
tcb    = PTR(tcb+132)           /* TCB (ISPTASK)         TCBOTC   */
fsa    = PTR(tcb+112)           /* first save area       TCBFSA   */
r1     = PTR(fsa+24)            /* ISPTASK's R1                   */
tld    = PTR(r1)                /* TLD address                    */
dta    = PTR(tld+076)           /* DTA address                    */
tbl    = STORAGE(D2X(dta+12),8) /* Name of temp table    DSL12345 */
"VGET ZENVIR"                   /* Retrieve ISPF Version          */
IF LEFT(Zenvir,8) < 'ISPF 5.9' THEN DO
  dtb    = PTR(dta+016)           /* DTB address                  */
  IF dtb=0 THEN
    dtb  = PTR(dta+044)           /* OPEN Table Chain             */
  IF STORAGE(D2X(dtb),3) /= 'DTB' THEN DO
    SAY 'Error: Table name could not be located (DTB)' zenvir
    EXIT 12
    END
  tbl  = STORAGE(D2X(dtb+6),8)  /* Name of temp table    DSL12345 */
  END

'CONTROL ERRORS RETURN'
'TBTOP' tbl
  IF rc>0 THEN DO
    SAY 'TBTOP failed for table' tbl 'RC='rc
    EXIT
  END

tally=0;rows=0;processed=0;zusize=0   /* Init counters */
DO FOREVER
  'TBSKIP' tbl                        /* NEXT ROW      */
  IF rc>0 THEN LEAVE
  'TBGET' tbl                         /* SET VARIABLES */
  IF rc>0 THEN LEAVE
  rows=rows+1                         /* count rows    */
  IF zusize = '' THEN ITERATE         /* ignore migrated data sets */
  tally=tally+zusize                  /* tally file size */
  processed=processed+1               /* count processed data sets */
END
ZMSG000S = tally 'Tracks'
ZMSG000L = 'Total Data sets:' rows,
           ' Data sets processed:' processed,
           ' Total tracks:' tally
"SETMSG MSG(ISPZ000)"
EXIT

PTR: RETURN C2D(STORAGE(D2X(ARG(1)),4))
