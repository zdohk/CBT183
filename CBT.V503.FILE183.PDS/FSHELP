/************************************************************** REXX **\
|*                                                                    *|
|* Module Name: FSHELP                                                *|
|*                                                                    *|
|* Descriptive Name:  Issue HELP command under ISPF and display       *|
|*                    output using the BROWSE service available       *|
|*                    in the STEMEDIT utility program.                *|
|*                                                                    *|
|* Status: R200                                                       *|
|*                                                                    *|
|* Function: This EXEC helps ISPF users trap the output of the        *|
|*           standard TSO/E HELP command and display its output       *|
|*           via the STEMEDIT routine.                                *|
|*                                                                    *|
|*           The user can then enter the V command to display the     *|
|*           result of the HELP command with VIEW instead of BROWSE.  *|
|*           Unlike BROWSE, VIEW allows the user to enter commands    *|
|*           such as CUT or CREATE, and EDIT macros, such as ROUTE.   *|
|*                                                                    *|
|*           FSHELP can only be used in ISPF, as a TSO command or     *|
|*           an EDIT macro.                                           *|
|*                                                                    *|
|* Author: Gilbert Saint-Flour <carlos@gsf-soft.com>                  *|
|*         http://gsf-soft.com/Freeware/                              *|
|*                                                                    *|
|* Syntax: FSHELP                                                     *|
|*           parameters    Name of TSO command and options            *|
|*                                                                    *|
|* Example:  FSHELP LISTCAT ALL          ; EDIT Macro                 *|
|*           TSO %FSHELP LISTDS          ; TSO Command                *|
|*                                                                    *|
|* Environment: TSO/E V2                                              *|
|*              ISPF/PDF 2.3                                          *|
|*              STEMEDIT  http://gsf-soft.com/Freeware/STEMEDIT.shtml *|
|*                                                                    *|
|*200 New version with STEMEDIT                                       *|
\**********************************************************************/
"ISPEXEC CONTROL ERRORS RETURN"        /* Prevent MACRO RC=20 ISRE771 */
"ISPEXEC ISREDIT MACRO (parms)"
IF rc>0 THEN ARG parms                      /* Invoked as TSO %FSHELP */
UPPER parms; cmd="HELP" parms
RC=OUTTRAP("LINE."); cmd; cc=rc; rc=OUTTRAP("OFF")

SIGNAL ON SYNTAX                            /* STEMEDIT not available */
CALL STEMEDIT "BROWSE","LINE.",,,cmd        /* Display HELP Output    */
EXIT cc

SYNTAX:                            /* STEMEDIT function not available */
  error=rc
  'ISPEXEC CONTROL DISPLAY LINE START(1)'
  SAY 'REXX error' error 'in line' sigl':' errortext(error)
  SAY sourceline(sigl)
  SAY ''
  DO i=1 TO QUEUED(); PARSE PULL line; SAY line ; END
  EXIT
