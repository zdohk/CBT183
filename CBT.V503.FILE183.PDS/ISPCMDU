/* ------------------------------------------------------- rexx
  Add commands to ISPF in-storage command table ISPCMDS.

  Parameters:

   DA(dsname(member))   Both dataset and membername are
                        optional. Default datasets is the
                        ISPPROF datases, default membername
                        is 'ISPCOMND'.
   QUIET                Do not show confirmation and statistics.
   ID(id)               set 'desc' field to id.

  History:

   2005.04.18 Initial
   2016.01.31 Retain aliases at top
              Use BPXWDYN instead of ALLOCATE

   ------------------------------------------------------------ */

 $table=''
 parse value ' 'translate(arg(1)) with p1 'DA('pmda')' p2
 if right(p2,1)=')' then p2=substr(p2,2)
 parse value p1 p2 with p1 'ID('pmid')' p2
 parse value p1 p2 with p1 ' QUIET ' +0 pmquiet p2
 if p1 p2<>'' then call close 8 'Invalid parm:' space(p1 p2)
 address isredit "MACRO NOPROCESS"
 Select
   when rc = 0 then call Edit_load /* edit macro */
   when pmda Â¬= "" then call Dsn_Load pmda
   otherwise call Dsn_Load Locate_Ispprof()
 end

 Address IspExec
 "control errors return"

 Call MakeWorkTable

 added=0
 updated=0
 deleted=0
 do i=1 to r.0
   c = translate(r.i)
   if word(c,1) = "/*" then iterate
   if c = ""           then iterate
   dlm=substr(c,1,1)
   parse var c (dlm) verb (dlm) trunc,
               (dlm) act  (dlm) desc (dlm) .
   upper verb act
   if dlm = '-' then call tbldel
   else call tblupd
 end

 Call RebuildTable    /* rewrite ISPCMDS */

 Call Close 0 'added('added') updated('updated') deleted('deleted')'

Close:
 parse arg _rc _msg
 Address TSO "delstack"
 if $table<>'' then Address IspExec "tbend" $table
 parse arg _rc _msg
 if _msg<>'' then call XMsg _msg
 Exit word(_rc 0,1)

TblUpd:
 if locate(verb) = 0 then do
   call updvars
   "tbput" $table
   updated=updated+1
   return
 end
 /* verb not found, add it */
 "tbtop" $table
 call updvars
 "tbadd" $table
 added=added+1
 return

TblDel:
 if Locate(verb) > 0 then return
 "tbdelete" $table
 deleted=deleted+1
 return

UpdVars:
 zctverb=verb
 zcttrunc=trunc
 zctact=act
 rtype='C'
 if desc = "" then desc = "*"pmid"*"
 zctdesc=desc
 return

Locate:
 "tbtop" $table
 "tbvclear" $table
 arg zctverb .
 "tbscan" $table "arglist(zctverb)"
 return rc

XMsg:
 if pmquiet<>'' then return 0
 parse arg zedlmsg
 if zedlmsg = '' then return 0
 zedlmsg= 'ISPCMDU' zedlmsg
 address ispexec "Vput (zedlmsg)"
 address ispexec "setmsg msg(isrz000)"
 return 0

Edit_Load:   /* load data to stem from edit session */
 do i=1 to 9999
   address isredit '(l) = line' i
   if rc > 0 then leave
   r.i = strip(l,t)
 end
 r.0=i-1
 return

Dsn_Load:   /* load from library */
 arg d m .
 d = strip(d,"b","'")
 if m = '' then m = 'ISPCOMND'
 d = "'"d"("m")'"
 address tso
 if sysdsn(d)<>'OK' then ,
   call close 8 'Could not locate file' d
 if BpxWdyn('alloc da('d') shr rtddn(xdd)')<>0 then,
   call close 8 'Allocate' d 'failed'
 "execio * diskr" xdd "(stem r. finis)"
 cc = rc
 zz=BpxWdyn('free dd('xdd')')
 if cc<>0 then call close 8 'Reading' d 'failed rc' cc
 return 0

Locate_Ispprof:   /* locate profile dataset */
 x=outtrap('ddl.')
 Address TSO "lista st"
 x=outtrap('off')
 do i=1 to ddl.0
   c = word(ddl.i,1)
   if pos(".",c) > 0 then iterate /* not ddname */
   if c = "ISPPROF" then do
     i = i-1
     return ddl.i
   end
 end
 return ""

MakeWorkTable:  /* copy system ISPCMDS */
 $table='T'left(space(translate(time('l'),' ','.:'),0),7)
 Address IspExec
 "tbcreate" $table "names(rtype zctverb zcttrunc zctact zctdesc)"
 "tbtop ispcmds"
 do forever
   "tbskip ispcmds"
   if rc<>0 then leave
   if word(zctact,1)='ALIAS' then rtype='A'  /* alias   */
   else rtype='C'                     /* command */
   "tbadd" $table
 end
 "tbtop" $table
 return 0

RebuildTable:  /* rewrite system ISPCMDS */
 Address IspExec
 /* clear system table */
 "tbbottom ispcmds"
 do forever
   "tbdelete ispcmds"
   if rc<>0 then leave
 end
 /* build system table */
 "tbsort" $table "FIELDS(RTYPE,C,A,ZCTVERB,C,A) "
 "tbtop"  $table
 do forever
   "tbskip" $table
   if rc<>0 then leave
   "tbadd ispcmds"
 end
 "tbtop ispcmds"
 return 0
 Call ListTable 'ispcmds'
 return 0

ListTable: Procedure   /* list in-storage command table */
 arg name
 address ispexec
 'tbtop' name
 i=1
 r.i='T' left("Verb",8,".") left("Len",3,"."),
     left("Action",50,".") left("Desc",15".")
 do forever
   'tbskip' name
   if rc > 0 then leave
   i=i+1
   r.i=left(rtype,1) left(zctverb,8) left(zcttrunc,3),
       left(zctact,50) left(zctdesc,15)
 end
 r.0=i
 Address TSO
 CALL STEMEDIT 'BROWSE','R.',,,'Command table' name
 return 0
