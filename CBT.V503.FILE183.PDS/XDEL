/*************************************************************** REXX */
/*                                                                    */
/* MODULE NAME = XDEL                                                 */
/*                                                                    */
/* DESCRIPTIVE NAME = REXX EDIT Macro - Delete current member         */
/*                                                                    */
/* STATUS = R300                                                      */
/*                                                                    */
/* FUNCTION = XDEL can be used as a TSO command or as an EDIT macro   */
/*            to delete a PDS member using the LMMDEL ISPF service.   */
/*                                                                    */
/* SYNTAX = See below                                                 */
/*                                                                    */
/*         EDIT Macro: XDEL member-name                               */
/*            Example: XDEL IEASYS33                                  */
/*                                                                    */
/*        When XDEL is invoked as an EDIT macro and no member name    */
/*        is specified, then the member being EDITed is deleted.      */
/*                                                                    */
/*        TSO Command: XDEL data.set.name member-name                 */
/*            Example: XDEL 'SYS1.PARMLIB' IEASYS33                   */
/*                                                                    */
/* Author: Gilbert Saint-Flour <carlos@gsf-soft.com>                  */
/*             http://gsf-soft.com/Freeware/                          */
/*                                                                    */
/**********************************************************************/
ADDRESS 'ISPEXEC'
"CONTROL ERRORS RETURN"
macro='NO'
"ISREDIT MACRO (PARM)"
IF rc=0 THEN DO               /* Invoked as an EDIT macro */
  zmemb=TRANSLATE(Parm)
  "ISREDIT (zdsn) = DATASET"
  zdsn="'"zdsn"'"
  IF zmemb = "" THEN DO
    "ISREDIT (zmemb) = MEMBER"
    IF zmemb = "" THEN EXIT 20
    Macro='YES'
    END
  END
ELSE                          /* Invoked as a TSO command */
  ARG zdsn zmemb
  /*---------------------------------------------------------------*/
  /*          Scratch the member                                   */
  /*---------------------------------------------------------------*/
"LMINIT DATAID(dataid) DATASET("zdsn") ENQ(SHRW)"
IF rc>0 THEN SIGNAL ERROR
"LMOPEN DATAID("dataid") OPTION(OUTPUT)"
IF rc > 0 THEN SIGNAL ERROR
"LMMDEL DATAID("dataid") MEMBER("zmemb")"
IF rc = 12 & macro='YES' THEN
  "LMMDEL DATAID("dataid") MEMBER("zmemb") NOENQ" /* ISPF V4 */
IF rc > 0 THEN SIGNAL ERROR
ZEDSMSG = "Member" zmemb "deleted"
ZEDLMSG = zmemb "was deleted in" zdsn
"SETMSG MSG(ISRZ000)"
  /*---------------------------------------------------------------*/
  /*          Clean-up                                             */
  /*---------------------------------------------------------------*/
"LMCLOSE DATAID("dataid")"
"LMFREE DATAID("dataid")"
IF macro='YES' THEN
  "ISREDIT CANCEL"
EXIT
  /*---------------------------------------------------------------*/
  /*          Error routine                                        */
  /*---------------------------------------------------------------*/
ERROR:
  "SETMSG MSG("ZERRMSG")"
  "LMCLOSE DATAID("dataid")"
  IF macro='NO' THEN "LMFREE DATAID("dataid")"
