//GILBERTH JOB (ACCT#),SCANMODL,
//*RESTART=HLASM2,
// NOTIFY=&SYSUID,
// CLASS=A,MSGCLASS=H,COND=(0,NE)
//HLASM EXEC PGM=ASMA90,PARM=(OBJECT,NODECK,NOESD,NORLD,NOXREF)
***********************************************************************
*                                                                     *
* MODULE NAME = SCANMODL                                              *
*                                                                     *
* DESCRIPTIVE NAME = RETRIEVE OFFSET & LENGTH OF A JES2 FIELD         *
*                                                                     *
* FUNCTION = THIS MODULE LOADS AN IPCS MODEL FROM SYS1.MIGLIB AND     *
*            RETURNS THE OFFSET & LENGTH OF A GIVEN FIELD.            *
*            IT IS USED TO WRITE PROGRAMS THAT ACCESS JES2 CONTROL    *
*            BLOCKS WITHOUT USING HASPSRC AND CONSEQUENTLY,           *
*            ARE RELEASE-INDEPENDENT.                                 *
*                                                                     *
* STATUS = R101                                                       *
*                                                                     *
* AUTHOR = GILBERT SAINT-FLOUR <carlos@gsf-soft.com>                  *
*                                                                     *
* INVOCATION = VIA CALL OR LINK                                       *
*                                                                     *
*          LA   R2,=CL8'HASMSDB'          HASP SDB                    *
*          LA   R3,=CL8'SDBRECCT'         RECORD COUNT                *
*          LINK EP=SCANMODL,PARAM=((R2),(R3))                         *
*          LH   R4,0(,R1)                 OFFSET                      *
*          LH   R5,2(,R1)                 LENGTH                      *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = MVS/ESA V4.1 OR ABOVE                             *
*                                                                     *
*    AUTHORIZATION = NONE                                             *
*                                                                     *
* MODULE TYPE = PROCEDURE, (SUB-ROUTINE)                              *
*                                                                     *
*    PROCESSOR = IBM OS/ASSEMBLER H VERSION 2 OR                      *
*                IBM HIGH LEVEL ASSEMBLER/MVS                         *
*                                                                     *
*    MODULE SIZE = 4K                                                 *
*                                                                     *
*    ATTRIBUTES = REENTERABLE, RMODE ANY, AMODE 31,                   *
*                 PROBLEM STATE, TCB KEY                              *
*                                                                     *
* OPERATION = SEE DOCUMENTATION                                       *
*                                                                     *
* CHANGE ACTIVITY                                                     *
*                                                                     *
*   @100 INITIAL VERSION                                              *
*   @101 MINOR CHANGES                                                *
*                                                                     *
***********************************************************************
SCANMODL CSECT
SCANMODL RMODE ANY
         B     BEGIN-*(,R15)
         DC    C'GSF UTILITIES - SCANMODL R101 '
BEGIN    BAKR  R14,0
         LR    R11,R15
         USING SCANMODL,R11
         LM    R8,R9,0(R1)             R8=HASMSDB,R9=SDBRECCT
         L     R1,16(,0)               CVTPTR
         L     R1,8(,R1)               CVTLINK
         LOAD  EPLOC=(R8),DCB=(R1),ERRET=RC12
         LR    R4,R0                   IM$SDB
         USING HEADER,R4
         SLR   R1,R1
         IC    R1,HDROFFSET            OFFSET TO 1ST FIELD DESC
         LA    R5,HEADER(R1)           ADDR OF 1ST FIELD DESC
         USING FLDDESC,R5
*LOOP
LOOP5    CLC   FLDNAME,0(R9)           REQUESTED FIELD?
         BE    LOOP5X                  YES, EXIT
         LA    R5,FLD_NEXT             NEXT FIELD DESCRIPTOR
         CLI   FLDDESC,0               END OF MODEL?
         BNE   LOOP5                   NO, PROCESS NEXT FIELD
*ENDLOOP
         LA    R15,8                   RC=8  (FIELD NOT FOUND)
         PR
*
*        RETURN OFFSET AND LENGTH IN SAVEAREA+8
*
LOOP5X   LA    R1,8(,R13)              4-BYTE WORK AREA CALLER'S AMODE
         MVC   0(4,R1),FLDOFFSET       MOVE OFFSET & LENGTH
         SLR   R15,R15                 RC=0
         PR
*
RC12     LA    R15,12                  RC=12 (LOAD FAILED)
         PR
*        BLSQCBM                   CONTROL BLOCK MODEL USED BY BLSQFORM
HEADER   DSECT
HDRACRO  DS    CL8                     EBCDIC CB ACRONYM
HDRUTHDR DS    CL8'$SDB'               OUTPUT HEADER
HDRMLVL  DS    CL8'HJE4430'            MAINTENANCE LEVEL
HDRCBLEN DS    FL2                     LENGTH OF CONTROL BLOCK
HDRAOFF  DS    FL2                     OFFSET TO ACRONYM
HDRALEN  DS    FL1                     LENGTH OF ACRONYM
HDRLLEN  DS    FL1                     LABEL LENGTH
HDRSCOL  DS    FL1                     START COLUMN OR LEFT MARGIN
HDRNCOL  DS    FL1                     NUMBER OF COLS BETWEEN LABELS
HDRFLAGS DS    XL1                     NUMBER OF COLS BETWEEN LABELS
HDROFFSET DS   FL1                     OFFSET TO 1ST FIELD DESC
HDRMODLVL DS   FL1                     MODEL LEVEL
          DS   X                       RESERVED
*
FLDDESC  DSECT
FLDNAME  DS    CL8                0    FIELD NAME
FLDFLAGS DS    X,X                8    FLAGS
FLDVIEW  DS    XL2               10    VIEW
FLDOFFSET DS   FL2               12    OFFSET
FLDLENGTH DS   FL2               14    LENGTH
FLD_NEXT EQU   *                 16
         YREGS
         END
//SYSLIB   DD DSN=SYS1.MACLIB,DISP=SHR
//SYSUT1   DD UNIT=VIO,SPACE=(CYL,2)
//SYSPRINT DD SYSOUT=*
//SYSLIN   DD UNIT=VIO,SPACE=(TRK,1),DISP=(,PASS),BLKSIZE=3200
//*
//LKED    EXEC PGM=IEWL,PARM=RENT
//SYSLIN   DD DSN=*.HLASM.SYSLIN,DISP=(OLD,DELETE)
//SYSLMOD  DD DSN=CBTTAPE.FILE183.LOAD(SCANMODL),DISP=SHR
//SYSPRINT DD SYSOUT=*
//*
//*      TEST PROGRAM - DISPLAY OFFSETS NEEDED BY SYSDEBUG
//*
//HLASM2 EXEC PGM=ASMA90,PARM=(OBJECT,NODECK,NOESD,NORLD,NOXREF)
TEMPNAME CSECT
         BAKR  R14,0
         LR    R12,R15
         USING TEMPNAME,R12
         LOAD  EP=SCANMODL             PRE-LOAD
         OPEN  (SYSPRINT,OUTPUT)
         LA    R2,=CL8'HASMSDB'
         LA    R3,=CL8'SDBDEB'
         BAL   R14,DISPLAY          <- DISPLAY SDBDEB
         LA    R3,=CL8'SDBUBF'
         BAL   R14,DISPLAY          <- DISPLAY SDBUBF
         LA    R2,=CL8'HASMBUFR'
         LA    R2,=CL8'HASMBFD'        HJE7707
         LA    R3,=CL8'BFDLOC'
         BAL   R14,DISPLAY          <- DISPLAY BFDLOC
         LA    R3,=CL8'TPBUFST'
         LA    R3,=CL8'BFDSTART'       HJE7707
         BAL   R14,DISPLAY          <- DISPLAY TPBUFST (BFDDATA)
         PR
*
DISPLAY  BAKR  R14,0
         LINK  EP=SCANMODL,PARAM=((R2),(R3))
         ST    R15,RC                  RETURN CODE
         MVC   OFFSET,0(R1)            OFFSET
         MVC   LENGTH,2(R1)            LENGTH
         STRING ' CB=',((R2),8),       CONTROL BLOCK                   X
               ' FLD=',((R3),8),       FIELD                           X
               ' RC=',(RC,F,R3Z),                                      X
               ' OFFSET=',(OFFSET,,X),                                 X
               ' LENGTH=',(LENGTH,,X),                                 X
               INTO=WORK121
         PUT   SYSPRINT,WORK121
         L     R15,RC                  RETURN CODE
         PR
*
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,RECFM=FBA,LRECL=121
RC       DS    F
OFFSET   DS    H                       OFFSET
LENGTH   DS    H                       LENGTH
WORK121  DS    CL121
         STRING GENERATE
         YREGS
         END
//SYSLIB   DD DSN=SYS1.MACLIB,DISP=SHR
//         DD DSN=CBTTAPE.FILE183.PDS,DISP=SHR            STRING
//SYSUT1   DD UNIT=VIO,SPACE=(CYL,2)
//SYSPRINT DD SYSOUT=*
//SYSLIN   DD UNIT=VIO,SPACE=(TRK,1),DISP=(,PASS),BLKSIZE=3200
//*
//GO      EXEC PGM=LOADER,PARM=NOPRINT
//STEPLIB  DD DSN=CBTTAPE.FILE183.LOAD,DISP=SHR           SCANMODL
//*DD DSN=SYS1.SHASMIG,DISP=SHR                           HASMSDB
//SYSLIN   DD DSN=*.HLASM2.SYSLIN,DISP=(OLD,DELETE)
//SYSPRINT DD SYSOUT=*
