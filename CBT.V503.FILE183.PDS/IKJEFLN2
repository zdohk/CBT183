//GILBERTL JOB (ACCT#),IKJEFLN2,
// NOTIFY=&SYSUID,
// CLASS=A,MSGCLASS=H,COND=(0,NE)
//HLASM EXEC PGM=ASMA90,PARM=(OBJECT,NODECK,NOESD,NORLD,NOXREF)
***********************************************************************
*                                                                     *
* MODULE NAME = IKJEFLN2                                              *
*                                                                     *
* DESCRIPTIVE NAME = TSO Logon Post-Display Exit for TN3270 Reconnect *
*                                                                     *
* FUNCTION = This TSO Logon exit allows LOGON RECONNECT to succeed    *
*            in the TN3270 environment by issuing a VTAM disconnect   *
*            command before Logon actually proceeds.                  *
*            The user must specify the Reconnect option on the        *
*            Logon panel and log onto the system on which the         *
*            active TSO session was created.                          *
*                                                                     *
* STATUS = R099f (Beta)                                               *
*                                                                     *
* AUTHOR = Gilbert Saint-Flour <carlos@gsf-soft.com>                  *
*          Thanks to Keith Moe of Amdahl for his suggestions and help.*
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = MVS/ESA or OS/390                                 *
*                                                                     *
*    AUTHORIZATION = APF                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
* MODULE TYPE = PROCEDURE, (TSO Logon Exit)                           *
*                                                                     *
*    PROCESSOR = IBM HIGH LEVEL ASSEMBLER/MVS                         *
*                                                                     *
*    MODULE SIZE = 1K                                                 *
*                                                                     *
*    ATTRIBUTES = RENT, REFR, RMODE ANY, AMODE 31,                    *
*                 SUPERVISOR STATE, KEY 8                             *
*                                                                     *
* INSTALLATION = This exit must be installed in an authorized system  *
*             library (LPA or link-list) under the name IKJEFLN2.     *
*             No SYS1.PARMLIB change is necessary to activate this    *
*             exit - its presence is a system library triggers its    *
*             invocation.                                             *
*             To deactivate this exit and remove it from the system,  *
*             simply delete it and refresh LLA if it was in a         *
*             link-list library.                                      *
*                                                                     *
* OPERATION = Retrieve the user-id and password from the input        *
*             parameters and check all of the active address spaces   *
*             for an existing TSO session with the specified user-id. *
*                                                                     *
*             If a TSO session is found, then check the TSBXWREC      *
*             flag which indicates that the terminal has been         *
*             disconnected and the session is already in "waiting     *
*             to reconnect" status. If TSBXWREC=1, exit.              *
*                                                                     *
*             If TSBXWREC=0, then validate the password by issuing    *
*             RACROUTE REQUEST=VERIFY.                                *
*                                                                     *
*             If RACROUTE fails, prompt the user for the correct      *
*             password.                                               *
*                                                                     *
*             If RACROUTE is successful, issue a VARY NET,TERM        *
*             command to disconnect the TSO session from the          *
*             terminal and and exit to the Logon processor.           *
*                                                                     *
* Notes:      This version of the exit allows Reconnect to proceed    *
*             when the screen size of the new terminal is smaller     *
*             than the size of the old terminal.  This should not     *
*             be a problem if the session is at the READY prompt      *
*             because TSO automatically adjusts to the new screen     *
*             size.  Reconnecting with a smaller screen size,         *
*             however, is likely to cause screen writing errors       *
*             in ISPF and other TSO full-screen applications.         *
*             Note that the same problem exists with 3270 terminals.  *
*                                                                     *
*             This exit will not allow a user to reconnect to         *
*             a session that is active on a different member          *
*             of a MAS or SYSPLEX.                                    *
*                                                                     *
* CALLED BY = TSO Logon (re: TSO/E Customization manual).             *
*                                                                     *
* CHANGE ACTIVITY                                                     *
*                                                                     *
* 099f 5th Beta Release                                               *
*                                                                     *
***********************************************************************
IKJEFLN2 CSECT
IKJEFLN2 RMODE ANY
         DC    C'GSF Utilities - IKJEFLN2 R099f '
BEGIN    BAKR  R14,0                   SAVE THE REGISTERS
         LR    R11,R1                  parm list
         USING PARMLIST,R11
         LR    R12,R15                 local base
         USING BEGIN,R12
*
*        check that the parm list contains reconnect=S, otherwise exit.
*
CHKRECON L     R2,PARM31               reconnect option
         ICM   R0,B'1111',0(R2)        parm omitted?
         BZ    EXIT                    yes, exit
         ICM   R1,B'1111',4(R2)        length=0?
         BZ    EXIT                    yes, exit
         CLI   8(R2),C'S'              reconnect=S ?
         BNE   EXIT                    no, exit
*
*        allocate dynamic storage area
*
GETDYNAM GETMAIN R,LV=DYNAML           allocate dynamic storage
         ST    R13,4(,R1)
         ST    R1,8(,R13)
         LR    R13,R1                  A(DYNAM)
         USING DYNAM,R13
         MVC   VTAMCMD(VTAMCMDL),VTAMCMDM
*
*        retrieve user-id from parm list
*
GETUID   LA    R0,USERID               "to" area
         LA    R1,L'USERID             "to" length
         L     R2,PARM21               user-id
         LA    R14,8(,R2)              "from" area
         LH    R15,6(,R2)              "from" length
         ICM   R15,B'1000',BLANKS      padding character
         MVCL  R0,R14                  move user-ID
*---------------------------------------------------------------------*
*                                                                     *
*        Search ASCB chain for an active TSO session                  *
*        with the same user-ID.                                       *
*                                                                     *
*---------------------------------------------------------------------*
FINDTSB  LA    R4,2                    FIRST ASID
         L     R5,CVTPTR(0,0)          CVT ADDRESS
         L     R5,CVTASVT-CVTMAP(,R5)  POINT TO ASVT
         L     R5,ASVTMAXU-ASVT(,R5)   MAX ASID
*LOOP
LOOP2    LOCASCB ASID=(R4)
         LTR   R15,R15                 ASCB active?
         BNZ   LOOP2NXT                no, ignore
         CL    R1,PSAAOLD-PSA(0,0)     own address space?
         BE    LOOP2NXT                yes, ignore
         LR    R7,R1                   pass ASCB address
         USING ASCB,R7
         ICM   R8,B'1111',ASCBTSB      TSO ADDRESS SPACE?
         BZ    LOOP2NXT                no, ignore
         L     R1,ASCBJBNS             point at job name
         CLC   USERID,0(R1)            MY USERID?
         BE    ASIDF99                 YES, EXIT
LOOP2NXT LA    R4,1(,R4)               NEXT ASID
         BCT   R5,LOOP2                NEXT ASID
*ENDLOOP
         B     CLEANUP                 No active TSO session found
ASIDF99  EQU   *
*---------------------------------------------------------------------*
*                                                                     *
*        Retrieve TSB & TSBX associated with the TSO session.         *
*                                                                     *
*---------------------------------------------------------------------*
         USING TSB,R8
MOVETSB1 LA    R0,L'TSB1-1             LENGTH OF UNPROTECTED TSB
         IVSK  R1,R8                   Source key into R1
         MVCSK TSB1,TSB                Move TSB to unprotected storage
         LA    R8,TSB1
         L     R9,TSBEXTNT             addr of the TSBX
         LA    R0,L'TSBX1-1            LENGTH OF UNPROTECTED TSBX
         IVSK  R1,R9                   Source key into R1
         MVCSK TSBX1,0(R9)             Move TSBX to unprotected storage
         USING TSBX,TSBX1
*
*        If the TSO session is already in "waiting to reconnect"
*        status, then exit.
*
         TM    TSBXFLG1,TSBXWREC       waiting to reconnect?
         BO    CLEANUP                 yes, we're done
         TM    TSBXFLG2,TSBXPREC       Reconnect in progress?
         BO    CLEANUP                 yes, we're done
*---------------------------------------------------------------------*
*                                                                     *
*        Validate the userid/password using RACROUTE REQUEST=VERIFY   *
*        and the current terminal name.                               *
*                                                                     *
*        If the user specified a Group name, it is not validated      *
*        here but will be validated later on by Logon processing.     *
*                                                                     *
*---------------------------------------------------------------------*
CHKPSWD  MVC   RACINITL(RACINITLL),RACINITM
         L     R2,PARM32               OID card
         ICM   R0,B'1111',0(R2)        any data specified here?
         BZ    CHKPSWD5                no, ignore
         RACINIT OIDCARD=7(R2),        OID Card                        X
               MF=(M,RACINITL)
CHKPSWD5 EQU   *
         LM    R2,R3,PARM21            user-id,password
         ICM   R0,B'1111',0(R3)        was password specified?
         BZ    CLEANUP                 no, exit
         RACINIT ENVIR=CREATE,                                         X
               USERID=7(R2),           user-id                         X
               PASSWRD=7(R3),          password                        X
               TERMID=TERMID1,         Own terminal name (current)     X
               ACEE=ACEEPTR,           addr of the ACEE                X
               MF=(E,RACINITL)
         LTR   R2,R15                  RACINIT successful?
         BNZ   BADPSWD                 No, error
*
*        Delete the ACEE we just created, it's useless.
*
         MVC   RACINITL(RACINITLL),RACINITM
         RACINIT ENVIR=DELETE,         DELETE ACEE                     X
               ACEE=ACEEPTR,           addr of the ACEE                X
               MF=(E,RACINITL)
*---------------------------------------------------------------------*
*                                                                     *
*        Retrieve current terminal name from my own TSB               *
*                                                                     *
*---------------------------------------------------------------------*
MOVETSB2 L     R7,PSAAOLD-PSA(0,0)     current ASCB
         L     R8,ASCBTSB              Terminal Status Block
         LA    R0,L'TSB2-1             LENGTH OF UNPROTECTED TSB
         IVSK  R1,R8                   Source key into R1
         MVCSK TSB2,TSB                Move TSB to unprotected storage
         LA    R8,TSB2
         MVC   SIZE1,TSBPRMR           save screen sizes
         L     R9,TSBEXTNT             addr of the TSBX
         LA    R0,L'TSBX2-1            LENGTH OF UNPROTECTED TSBX
         IVSK  R1,R9                   Source key into R1
         MVCSK TSBX2,0(R9)             Move TSBX to unprotected storage
*---------------------------------------------------------------------*
*                                                                     *
*        Issue VTAM disconnect command                                *
*                                                                     *
*---------------------------------------------------------------------*
SENDCMD  MVC   NEWTERM,TERMID1         new terminal name
         MVC   OLDTERM,TERMID2         old terminal name
         MODESET KEY=ZERO
         SLR   R0,R0                   CN(0)
         MGCR  VTAMCMD                 issue command
         MODESET KEY=NZERO
*
         TPUT  VTAMCMD+4,VTAMCMDL-4    display command
NEXTPAGE MVC   WORK80(3),=C'***'       move string to 24-bit storage
         TPUT  WORK80,3,ASIS           display ***
         TGET  WORK80,1                prompt the user to press ENTER
         B     CLEANUP
*---------------------------------------------------------------------*
*                                                                     *
*        RACINIT Failed                                               *
*                                                                     *
*        If the password is invalid, set up control switches          *
*        to ensure the user is prompted for a new password.           *
*                                                                     *
*---------------------------------------------------------------------*
BADPSWD  CH    R2,=H'8'                invalid password?
         BNE   CLEANUP                 no, exit
*
         L     R3,PARM10               control switches
         OI    4+8(R3),BIT0            re-prompt
         L     R4,PARM15               re-prompt code
         LA    R1,2                    password code
         ST    R1,8(,R4)               store code into parm15
*
*        Set the "do not issue RACROUTE" switch to
*        prevent RACF from counting two violations.
*
         OI    2+8(R3),BIT2            do not issue RACROUTE
*---------------------------------------------------------------------*
*                                                                     *
*        free dynamic storage and exit                                *
*                                                                     *
*---------------------------------------------------------------------*
CLEANUP  FREEMAIN R,LV=DYNAML,A=(R13)
EXIT     SLR   R15,R15                 RC=0
         PR                            go back to IKJEFLE
RACINITM RACINIT MF=L
VTAMCMDM WTO   'VARY NET,TERM,TYPE=COND,NOTIFY=NO,SLU=12345678 1234567 X
               12345678',MF=L
VTAMCMDL EQU   *-VTAMCMDM
BLANKS   DC    CL8' '
***********************************************************************
*        Dynamic Save Area                                            *
***********************************************************************
DYNAM    DSECT
         DS    18F                     standard save area
VTAMCMD  DS    XL(VTAMCMDL)
         ORG   VTAMCMD+VTAMCMDL-25
OLDTERM  DS    CL8,C                   old terminal
USERID   DS    CL8                     user-id (8 chars)
NEWTERM  DS    CL8                     new terminal
         ORG
PASSWORD DS    CL8                     password
RACINITL RACINIT MF=L
RACINITLL EQU  *-RACINITL
ACEEPTR  DS    A                       addr of ACEE created by RACINIT
TSB1     DS    XL(TSB_LENGTH)          TSB of active TSO session
SIZE2    EQU   TSBPRMR-TSB+TSB1,4      screen size (old)
TERMID2  EQU   TSBTRMID-TSB+TSB1,8     terminal id (old)
TSBX1    DS    XL(TSBX_LENGTH)         TSBX of active TSO session
TSB2     DS    XL(TSB_LENGTH)          own TSB  (current)
SIZE1    EQU   TSBPRMR-TSB+TSB2,4      screen size (current)
TERMID1  EQU   TSBTRMID-TSB+TSB2,8     terminal id (current)
TSBX2    DS    XL(TSBX_LENGTH)         own TSBX (current)
WORK80   DS    CL80                    TPUT
DYNAML   EQU   *-DYNAM
***********************************************************************
*        Parameter list                                               *
***********************************************************************
PARMLIST DSECT
PARM1    DS    A                       Command Buffer
PARM2    DS    A                       New Command Buffer
PARM3    DS    A                       UPT
PARM4    DS    A                       ECT
PARM5    DS    A                       PSCB
PARM6    DS    A                       exit-to-exit communication word
PARM7    DS    A                       exit reason code
PARM8    DS    A                       reserved
PARM9    DS    A                       reserved
*                                      exit-dependent parameter list
PARM10   DS    A                       control switches
PARM11   DS    A                       Panel Module
PARM12   DS    A                       TGET Buffer
PARM13   DS    A                       Help Panel Number
PARM14   DS    A                       Language Code
PARM15   DS    A                       re-prompt code
PARM16   DS    A                       Installation-defined parm list
PARM17   DS    A                       reserved
PARM18   DS    A                       reserved
PARM19   DS    A                       First Message
PARM20   DS    A                       Second Message
PARM21   DS    A                       User-ID
PARM22   DS    A                       Password
PARM23   DS    A                       Account
PARM24   DS    A                       Procedure
PARM25   DS    A                       Region
PARM26   DS    A                       Performance Group
PARM27   DS    A                       New Password
PARM28   DS    A                       RACF Group Name
PARM29   DS    A                       No Mail Option
PARM30   DS    A                       No Notices Option
PARM31   DS    A                       Reconnect Option
PARM32   DS    A                       OID Card
PARM33   DS    A                       First Command
PARM34   DS    A                       SECLABEL
         PRINT NOGEN
PRINT    OPSYN ANOP
         YREGS                         R0-R15
         IEZBITS
PSA      DSECT
PSAAOLD  EQU   PSA+X'224',4,C'A'       current ASCB
         CVT   DSECT=YES               COMMUNICATION VECTOR TABLE
         IHAASCB                       ADDRESS SPACE CONTROL BLOCK
         IHAASVT                       ADDRESS SPACE VECTOR TABLE
         IKJTSB EXT=YES                TSB/TSBX
TSBX_LENGTH EQU *-TSBX
TSB      DSECT
TSB_LENGTH EQU *-TSB
         END   BEGIN
//SYSLIB   DD DSN=SYS1.MACLIB,DISP=SHR
//         DD DSN=SYS1.MODGEN,DISP=SHR             IEZBITS IHAASVT
//SYSUT1   DD UNIT=VIO,SPACE=(CYL,2)
//SYSPRINT DD SYSOUT=*
//SYSLIN   DD UNIT=VIO,SPACE=(TRK,1),DISP=(,PASS),DCB=BLKSIZE=3200
//*
//LKED    EXEC PGM=LINKLLA,PARM=(MAP,RENT)
//SYSLIN   DD DSN=*.HLASM.SYSLIN,DISP=(OLD,PASS)
//SYSPRINT DD SYSOUT=*
//SYSLMOD  DD DSN=SYS1.LOCAL.LINKLIB(IKJEFLN2),DISP=SHR
//
//
//*
//* Delete the module; as long as LLA is not refreshed,
//* it will remain available.  In case of a serious problem,
//* refreshing LLA will get rid of it.
//*
//SCRATCH EXEC PGM=IKJEFT01
//SYSTSPRT DD SYSOUT=*
//SYSTSIN  DD *
TSOLIB ACT DA('GSFSOFT.SCRATCH.LOAD')
SCRATCH 'SYS1.LOCAL.LINKLIB(IKJEFLN2)' LLAREFRESH
/*
