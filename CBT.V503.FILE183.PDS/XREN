/*************************************************************** REXX */
/*                                                                    */
/* MODULE NAME = XREN                                                 */
/*                                                                    */
/* DESCRIPTIVE NAME = Rename Current Member (EDIT Macro)              */
/*                                                                    */
/* FUNCTION = This EDIT macro renames the member being edited         */
/*  to the specified new name.                                        */
/*                                                                    */
/*  If a new name is not specified in the command, the member name    */
/*  at the cursor position is extracted and used as the new name.     */
/*                                                                    */
/*  If "XREN newname END" or "XREN newname SAVE" is specified,        */
/*  then an END or SAVE command is issued after the successful        */
/*  completion of the rename operation.                               */
/*                                                                    */
/* STATUS = R200                                                      */
/*                                                                    */
/* Author: Gilbert Saint-Flour <carlos@gsf-soft.com>                  */
/*                                                                    */
/**********************************************************************/
ADDRESS ISPEXEC; "ISREDIT MACRO (NEWNM ENDSAVE)"

   /* If the new name is not specified in the command,  */
   /* extract the member name at the cursor position    */

IF newnm='' THEN DO
  "ISREDIT (LINE,CSRP) = CURSOR"      /*   TO RETRIEVE THE LINE AND   */
  "ISREDIT (LINE) = LINE .ZCSR"       /*   CURSOR POSITION            */
  IF CSRP>0 THEN DO                   /*   THEN                       */
    P = POS(' ',LINE,CSRP); IF P>CSRP THEN LINE=LEFT(LINE,P-1)
    P = POS(',',LINE,CSRP); IF P>CSRP THEN LINE=LEFT(LINE,P-1)
    P = POS('.',LINE,CSRP); IF P>CSRP THEN LINE=LEFT(LINE,P-1)
    P = POS(')',LINE,CSRP); IF P>CSRP THEN LINE=LEFT(LINE,P-1)
    P = POS('(',LINE,CSRP); IF P>CSRP THEN LINE=LEFT(LINE,P-1)
    LINE = REVERSE(LINE)
    P = POS(' ',LINE); IF P>1 THEN LINE=LEFT(LINE,P-1)
    P = POS(',',LINE); IF P>1 THEN LINE=LEFT(LINE,P-1)
    P = POS('.',LINE); IF P>1 THEN LINE=LEFT(LINE,P-1)
    P = POS('=',LINE); IF P>1 THEN LINE=LEFT(LINE,P-1)
    P = POS('(',LINE); IF P>1 THEN LINE=LEFT(LINE,P-1)
    P = POS(')',LINE); IF P>1 THEN LINE=LEFT(LINE,P-1)
    newnm = REVERSE(LINE)
  END
END

         /* If a new name is not available, issue error messsage */

IF newnm = "" THEN DO
  ZEDSMSG='New Name Missing'
  ZEDLMSG='A new member name was not specified in the command'
  "SETMSG MSG(ISRZ000)"
  EXIT 12
  END

         /* Rename the member using the LMMREN service */

"CONTROL ERRORS RETURN"
"ISREDIT (DD1) = DATAID"
"ISREDIT (MBR) = MEMBER"
"LMOPEN DATAID("DD1") OPTION(OUTPUT)"
IF RC > 0 THEN SIGNAL ERROR
"LMMREN DATAID("DD1") MEMBER("MBR") NEWNAME("newnm")"
IF RC = 12 THEN
  "LMMREN DATAID("DD1") MEMBER("MBR") NEWNAME("newnm") NOENQ"
IF RC > 0 THEN SIGNAL ERROR
ZEDSMSG = "Member renamed"
ZEDLMSG = "Member" mbr "renamed to" TRANSLATE(newnm)
"SETMSG MSG(ISRZ000)"
"LMCLOSE DATAID("DD1")"

         /* Optionally Issue END or SAVE then EXIT */

IF endsave Â¬= '' THEN
  "ISREDIT" TRANSLATE(endsave)
EXIT

         /* Error, issue error message */

ERROR:
  "SETMSG MSG("ZERRMSG")"
  "LMCLOSE DATAID("DD1")"
  EXIT 20
