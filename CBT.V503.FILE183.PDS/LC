/*************************************************************** REXX *\
|*                                                                    *|
|* Module Name: LC (ListCat)                                          *|
|*                                                                    *|
|* Descriptive Name:  Issue LISTCAT command and display output        *|
|*                    using the ISPF BRIF option.                     *|
|*                                                                    *|
|* Status: R201                                                       *|
|*                                                                    *|
|* Function: This EXEC helps ISPF users trap the output of the        *|
|*           LISTCAT command and display it via the STEMVIEW routine. *|
|*           If the user enters the V command, then STEMVIEW displays *|
|*           the result of the LISTCAT command with VIEW instead      *|
|*           of BROWSE.  VIEW allows commands such as CUT or CREATE,  *|
|*           and EDIT macros, such as ROUTE.                          *|
|*                                                                    *|
|*           LC can be used here :                                    *|
|*             Opt 11 : ISPF WorkPlace              (WP)              *|
|*             Opt 3.4 : ISPF Data Set List Utility (DSLIST)          *|
|*             Opt 6 : ISPF Command Shell           (ICS)             *|
|*                                                                    *|
|* Author: Gilbert Saint-Flour <carlos@gsf-soft.com>                  *|
|*         http://gsf-soft.com/Freeware/                              *|
|*                                                                    *|
|* Syntax: LC                                                         *|
|*             dsname        data set name                            *|
|*             options       ALL NAME HISTORY VOLUME                  *|
|*                                                                    *|
|* Example:  LC 'ibmuser.load' ALL                                    *|
|*           LC 'ibmuser.load' VOLUME                                 *|
|*                                                                    *|
|* Environment: TSO/E V2                                              *|
|*              ISPF/PDF 2.3                                          *|
|*              STEMVIEW  http://gsf-soft.com/Freeware/STEMEDIT.shtml *|
|*                                                                    *|
|*200 New version (STEMVIEW)                                          *|
|*201 Check for missing data set name                                 *|
\**********************************************************************/
PARSE UPPER ARG dsname options
IF dsname='' THEN DO                        /* dsname is missing      */
  ZEDSMSG = 'Missing Data Set Name'
  ZEDLMSG = 'Data Set Name is required but was not specified'
  "ISPEXEC SETMSG MSG(ISRZ001)"
  EXIT 20
  END
IF options='' THEN options='ALL'            /* Default Options        */
cmd="LISTCAT ENT("dsname")" options
RC=OUTTRAP("LINE."); cmd; cc=rc; rc=OUTTRAP("OFF")

SIGNAL ON SYNTAX                            /* STEMDISP not available */
CALL STEMDISP "BROWSE","LINE.",,,cmd        /* Display LISTCAT Output */
EXIT cc

SYNTAX:                            /* STEMDISP function not available */
  error=rc
  'ISPEXEC CONTROL DISPLAY LINE START(1)'
  SAY 'REXX error' error 'in line' sigl':' errortext(error)
  SAY sourceline(sigl)
  SAY ''
  DO i=1 TO QUEUED(); PARSE PULL line; SAY line ; END
  EXIT
