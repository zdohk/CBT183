/*------------------------------------------------------------ REXX --*\
|*                                                                    *|
|* MODULE NAME = SHOWTIOT                                             *|
|*                                                                    *|
|* DESCRIPTIVE NAME = Display TIOT Entries (Allocated Files)          *|
|*                                                                    *|
|* STATUS = R100                                                      *|
|*                                                                    *|
|* FUNCTION = The SHOWTIOT exec scans the TIOT and displays active    *|
|*            entries, particularly their dsname and volser.          *|
|*                                                                    *|
|*            The output is a subset of what's produced by the        *|
|*            LISTALC TSO cmd and by ISRDDN, an ISPF utility          *|
|*            program invoked via the DDLIST cmd.                     *|
|*                                                                    *|
|*            The primary interest in SHOWTIOT is how to retrieve     *|
|*            information about allocated files in REXX, and in       *|
|*            COBOL or PL/I as well.                                  *|
|*                                                                    *|
|*            This version of SHOWTIOT does NOT support               *|
|*            multi-volume TIOT entries.                              *|
|*                                                                    *|
|* AUTHOR   = Gilbert Saint-Flour <carlos@gsf-soft.com>               *|
|*            http://gsf-soft.com/Freeware/                           *|
|*                                                                    *|
|* DEPENDENCIES = TSO/E V2                                            *|
|*                SWAREQ REXX Routine                                 *|
|*                                                                    *|
|* SYNTAX   = SHOWTIOT                                                *|
|*                                                                    *|
\*--------------------------------------------------------------------*/
tcb = C2D(STORAGE(21C,4))                   /* TCB          PSATOLD  */
tiot= C2D(STORAGE(D2X(tcb+12),4))           /* TIOT         TCBTIO   */
n=0
NUMERIC DIGITS 10
SAY 'DDNAME  ' Left('Data Set Name',44) 'VOLSER TIOENTRY  SVA     JFCB'
Do Until STORAGE(D2X(tiot+24),1) = '00'X
  sva = STORAGE(D2X(tiot+24+12),3)          /* JFCB         TIOEJFCB */
  jfcb=SWAREQ(sva)                     /* convert SVA to 31-bit addr */
  ddn=STORAGE(D2X(tiot+24+4),8)             /* DDname       IOCBDSNM */
  If jfcb>16 & Left(ddn,1)>'00'x Then Do
    dsn=STORAGE(D2X(jfcb),44)               /* dsname       JFCBDSNM */
    vol=STORAGE(D2X(jfcb+118),6)            /* volser       JFCBVOLS */
    tioelink=STORAGE(D2X(tiot+24+3),1)      /* Flags        TIOELINK */
    If STORAGE(D2X(tiot+24+1),1)<'80'X Then Do /* TIOESTTA  TIOSLTYP */
      if vol='      ' THEN Do
        vol='VIO   '
        If tioelink='02'X Then
          vol='SYSOUT'
        End
      SAY ddn dsn vol C2X(STORAGE(D2X(tiot+24),4)) C2X(sva) D2X(jfcb)
      /* C2X(tioelink) */
      End
    End
  tiot=tiot+20                              /* next TIOT entry       */
  n=n+1; if n=111 Then Exit 20              /* prevent endless loop  */
END
Exit
  /* To make SHOWTIOT work faster, copy the SWAREQ routine here   */
  /* and activate the first line which contains SWAREQ: PROCEDURE */
