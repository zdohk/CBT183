/*************************************************************** REXX */
/*                                                                    */
/* MODULE NAME = VALLOC                                               */
/*                                                                    */
/* DESCRIPTIVE NAME = Generate ALLOCATE commands from data set list   */
/*                                                                    */
/* STATUS = R101                                                      */
/*                                                                    */
/* Author: Gilbert Saint-Flour <carlos@gsf-soft.com>                  */
/*         http://gsf-soft.com/Freeware/                              */
/*                                                                    */
/* FUNCTION = This EDIT macro reads a list of data set names          */
/*  starting in pos 1 of each line and generates a set of             */
/*  corresponding ALLOCATE commands.  A data set name list can        */
/*  be created from the DSLIST panel using the VDL exec.              */
/*                                                                    */
/* DEPENDENCIES = TSO/E V2                                            */
/*                ISPF V2.3 or above                                  */
/*                STEMVIEW or STEMEDIT                                */
/*                                                                    */
/**********************************************************************/
ADDRESS ISPEXEC
"CONTROL ERRORS RETURN"
"ISREDIT MACRO (TRAIL)"

"ISREDIT (I) = LINENUM .ZFIRST"
"ISREDIT (LASTLINE) = LINENUM .ZLAST"

QUEUE "//VALLOCJ  JOB (ACCT#),LOCAL.VALLOC,"
QUEUE "// NOTIFY=&SYSUID,REGION=2M,"
QUEUE "// CLASS=A,MSGCLASS=H,COND=(0,NE)"
QUEUE "//*"
QUEUE "//ALLOC   EXEC PGM=IKJEFT01,DYNAMNBR=200"
QUEUE "//SYSTSPRT DD SYSOUT=*"
QUEUE "//SYSTSIN  DD *"

tracks=0; datasets=0; aliases=0

DO i=1 to lastline
  "ISREDIT (line) = LINE" I             /* GET current data set name */
  dsn=TRANSLATE(WORD(line,1))           /* extract data set name */
  rc=LISTDSI("'" || dsn || "'" 'DIR' 'NORECALL')
  say dsn rc sysdsname syscreate "'" || sysrecfm || "'" sysdsorg
  IF rc>4 THEN ITERATE                  /* MIGRAT */
  IF sysdsorg='VS' THEN ITERATE         /* VSAM */
  IF sysalloc > sysprimary THEN
    sysprimary=sysalloc                 /* num of trks used */
  QUEUE " "                             /* blank line */
  IF dsn=sysdsname THEN DO
    QUEUE "ALLOCATE DSNAME('"sysdsname"') NEW CATALOG +"
    QUEUE "           UNIT(SYSALLDA) VOLUME(WORK01) +"
    IF sysrecfm='U' THEN
      dcb="RECFM(U) BLKSIZE("sysblksize")"
    ELSE DO
      recfm=LEFT(STRIP(sysrecfm),3)
      recfm=SUBSTR(recfm,1,1) SUBSTR(recfm,2,1) SUBSTR(recfm,3,1)
      dcb="RECFM("STRIP(recfm)") LRECL("syslrecl")"
      dcb=dcb "DSORG("sysdsorg")"
      END
    IF sysunits='BLOCK' THEN
      sysunits='BLOCK(' || sysblksize || ')'
    IF SYSDSSMS='DATA_LIBRARY' | SYSDSSMS='PROGRAM_LIBRARY' THEN
      dcb=dcb 'DSNTYPE(LIBRARY)'
    IF sysadirblk='' | sysadirblk='NO_LIM' THEN
      line="          SPACE("sysprimary','sysseconds")" sysunits dcb
    ELSE
      line="          SPACE("sysprimary','sysseconds")" sysunits,
                        "DIR("sysadirblk")" dcb
    IF LENGTH(line)>72 THEN
      line=RIGHT(LINE,72)
    QUEUE line
    tracks=tracks+sysprimary
    datasets=datasets+1
    END
  ELSE DO                              /* ALIAS */
    QUEUE "DEFINE ALIAS(NAME('"dsn"') -"
    QUEUE "           RELATE('"sysdsname"'))"
    aliases=aliases+1
    END
END

QUEUE " "
QUEUE " /* datasets="datasets "aliases="aliases "tracks="tracks "*/"
QUEUE " "

CALL STEMVIEW 'VIEW',,,,'ALLOCATE Commands'

EXIT rc
