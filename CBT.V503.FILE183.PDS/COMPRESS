/*REXX*****************************************************************/
/*                                                                    */
/* MODULE NAME = COMPRESS                                             */
/*                                                                    */
/* DESCRIPTIVE NAME = COMPRESS EDIT Macro for ISPF/PDF                */
/*                                                                    */
/* STATUS = R305                                                      */
/*                                                                    */
/* FUNCTION = The COMPRESS EDIT macro invokes the COMPRESS command    */
/*            for the current data set or for another data set,       */
/*            if a dsname is specified in the command.                */
/*                                                                    */
/* Author   =  Gilbert Saint-Flour <carlos@gsf-soft.com>              */
/*             http://gsf-soft.com/Freeware/                          */
/*                                                                    */
/* Dependencies = COMPRESS Command Processor R313 (COMPRCMD)          */
/*                STEMVIEW Program R105 (BATCH option only)           */
/*                                                                    */
/* Syntax   =  COMPRESS                                               */
/*                 dsname|*              data set name                */
/*                 BATCH|JCL             generate batch job           */
/*                                                                    */
/*        Note: the default dsname is the name of the current data    */
/*              set.  The * is only required when the BATCH option    */
/*              is specified for the current data set.  JCL is        */
/*              equivalent to BATCH.                                  */
/*                                                                    */
/* Change Activity                                                    */
/*                                                                    */
/* 300 New version, uses SETMSG option instead of OUTTRAP             */
/* 301 Simplify dsname retrieval                                      */
/* 302 Issue a long message instead of a short one                    */
/* 303 Add BATCH parm for background execution (requires STEMVIEW)    */
/* 304 Add optional HBACKDS capability through a switch               */
/*     Slash (/) means the same as asterisk.                          */
/*     Use LRECL(80) RECFM(F) PROFILE(CNTL) for STEMVIEW              */
/* 305 Issue error message when COMPRESS command is missing           */
/*                                                                    */
/**********************************************************************/
ADDRESS 'ISPEXEC'
ARG parms
IF parms /= '' THEN DO
  ZEDSMSG = 'Not an EDIT Macro'
  ZEDLMSG = 'COMPRESS is not invoked as an EDIT macro.'
      /* May occur when the COMPRESS command is missing
         and the COMPRESS EDIT macro re-invokes itself */
  "DISPLAY MSG(ISRZ001)"
  EXIT 20
  END
"ISREDIT MACRO (DSN BATCH)"
IF RC>0 THEN EXIT 20

UPPER dsn batch                        /* convert to upper-case      */
  /*----------------------------------------------------------------*\
  |*                Validate BATCH/JCL option                       *|
  \*----------------------------------------------------------------*/
jcl = ABBREV('BATCH',batch,1) | ABBREV('JCL',batch,1)
IF jcl=0 & batch/='' THEN DO
  ZEDSMSG = 'Invalid parameter'
  ZEDLMSG = 'Second parameter must be BATCH or JCL'
  "SETMSG MSG(ISRZ001)"
  EXIT 20
  END
  /*----------------------------------------------------------------*\
  |*                 Set HBACKDS switch                             *|
  \*----------------------------------------------------------------*/
hbackds = (SYSVAR(SYSHSM) /= '')       /* hbackds=1 if HSM active    */
hbackds = 0                           /* turn off HBACKDS capability */
  /*----------------------------------------------------------------*\
  |*                 Retrieve dsname and volser                     *|
  \*----------------------------------------------------------------*/
IF dsn='' | dsn='*' | dsn='/' THEN DO  /* Current Data Set           */
  "CONTROL ERRORS RETURN"              /* trap ISPEXEC error         */
  "ISREDIT (DSN) = DATASET"            /* retrieve dsname            */
  dsn="'" || dsn || "'"                /* Enclose dsname in quotes   */
  "ISREDIT (ID1) = DATAID"
  IF rc>4 THEN SIGNAL SETMSG
  "LMQUERY DATAID("ID1") VOLUME(vol) DATASET(dsn2)"
  IF rc>4 THEN SIGNAL SETMSG
  IF dsn2 /= '' THEN                   /* non-null                   */
    dsn=STRIP(dsn2)                    /* no trailing spaces         */
  IF vol /= '' THEN
    vol = 'VOLUME('vol')'
  END
ELSE                                   /* USER SPECIFIED A DSNAME    */
  vol=''
  /*----------------------------------------------------------------*\
  |*      Prefix TSO prefix if dsname is not fully qualified        *|
  \*----------------------------------------------------------------*/
IF LEFT(dsn,1)/="'" THEN DO
  "VGET ZPREFIX"                       /* get dsname prefix          */
  dsn= "'" || zprefix || '.' || dsn || "'"
  END
  /*----------------------------------------------------------------*\
  |*                Generate JCL for batch processing               *|
  \*----------------------------------------------------------------*/
IF jcl THEN DO
  "VGET ZACCTNUM"                             /* Accounting info   */
  jobname=LEFT(USERID()||'CPR',8)             /* JOB name          */
  acct=zacctnum                               /* Accounting info   */
  pgmr=USERID()                               /* Programmer's name */
  QUEUE '//' || jobname 'JOB' acct || ",'" || pgmr || "',"
  QUEUE '// NOTIFY=&SYSUID,COND=(0,NE),'
  QUEUE '// CLASS=A,MSGCLASS=H'
  QUEUE '//COMPRESS EXEC PGM=IKJEFT01,REGION=2M'
  QUEUE '//SYSTSPRT DD SYSOUT=*'
  QUEUE '//SYSTSIN DD *'
  IF hbackds THEN
    QUEUE 'HBACKDS' dsn 'WAIT'
  QUEUE 'COMPRESS' dsn 'SHR' vol
  CALL STEMVIEW 'VIEW',,,,'COMPRESS' dsn vol,,80,'F','CNTL'
  END
  /*----------------------------------------------------------------*\
  |*                     Foreground Compress                        *|
  \*----------------------------------------------------------------*/
ELSE DO                                /* BATCH was not specified */
  ZEDSMSG = ''
  IF hbackds THEN DO
    ZEDLMSG = 'Data Set' dsn 'is being backed up.'
    IF vol/='' THEN
      ZEDLMSG = 'Data Set' dsn 'is being backed up from' vol
    "CONTROL DISPLAY LOCK"
    "DISPLAY MSG(ISRZ001)"
    "SELECT CMD(HBACKDS" dsn vol "WAIT) MODE(FSCR)"
    END
  ZEDLMSG = 'Data Set' dsn 'is being compressed.'
  IF vol/='' THEN
    ZEDLMSG = 'Data Set' dsn 'is being compressed on' vol
  "CONTROL DISPLAY LOCK"
  "DISPLAY MSG(ISRZ001)"
  "SELECT CMD(COMPRESS" dsn vol "SHR SETMSG) MODE(FSCR)"
  END
EXIT

SETMSG:  IF zerrmsg/='' THEN 'SETMSG MSG('zerrmsg')' ; EXIT 20
