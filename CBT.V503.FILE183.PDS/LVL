/*************************************************************** REXX */
/*                                                                    */
/* MODULE NAME = LVL                                                  */
/*                                                                    */
/* DESCRIPTIVE NAME = Compress level numbers and adjust pos 79-80     */
/*                                                                    */
/* STATUS = R200                                                      */
/*                                                                    */
/* FUNCTION = This ISPF EDIT Macro compresses level numbers for       */
/*     FB-80 PDS members with STATS ON and NUMBER STD.                */
/*                                                                    */
/*     LVL reuses "gas levels", i.e. levels which are not used        */
/*     in any record in the member and adjusts pos 79-80 of the       */
/*     records accordingly.                                           */
/*                                                                    */
/*     If the macro invocation includes a number, then levels         */
/*     up to the specified number are reset to zero.                  */
/*                                                                    */
/* Author: Gilbert Saint-Flour <carlos@gsf-soft.com>                  */
/*                                                                    */
/**********************************************************************/
ADDRESS "ISPEXEC"
"ISREDIT MACRO (MIN)"
IF MIN='' THEN
  MIN=0
ELSE
  MIN=MIN+0
"ISREDIT (NUMMODE,NUMTYPE) = NUMBER"   /* QUERY NUMBER MODE INFO    */
IF NUMMODE='ON' THEN
  "ISREDIT NUMBER = OFF"               /* TURN OFF NUMBER MODE      */

DO LEV=0 TO 99; N.LEV=0 ; END          /* INIT ARRAY          */
HWM=0

'ISREDIT (ZLAST) = LINENUM .ZLAST'     /* LAST LINE           */
    /*--------------------------------------------------*\
    |*     DETERMINE WHICH LEVEL NUMBERS ARE USED       *|
    \*--------------------------------------------------*/
DO L=1 TO ZLAST
  "ISREDIT (LINE) = LINE" L                /* GET CURRENT LINE */
  IF RC>0 THEN EXIT RC
  LEV=RIGHT(LINE,2) + 0                /* EXTRACT LEVEL             */
  IF HWM<LEV THEN HWM=LEV              /* GET HIGHEST LEV           */
  N.LEV=N.LEV+1                        /* EXTRACT LEVEL             */
 /*  SAY LEV LINE N.LEV */
END
    /*---------------------------------------------*\
    |*          COMPRESS LEVEL NUMBERS             *|
    \*---------------------------------------------*/
RELEVEL=0
IF N.0 = 0 THEN
  N=-1
ELSE
  N=0
DO LEV=0 TO HWM
  IF N.LEV > 0 THEN DO
    IF LEV>0 THEN DO
      IF LEV > MIN THEN
        N=N+1
      ELSE DO
        N=0
        RELEVEL=1
        END
      END
    SAY RIGHT(LEV,3) RIGHT(N.LEV,5) 'N='N RELEVEL
    N.LEV=N                            /* NEW LEVEL NUMBER          */
    END
  ELSE DO                              /* THIS LVL IS NOT USED      */
    SAY RIGHT(LEV,3)
    RELEVEL=1
    END
END
    /*--------------------------------------------------*\
    |*              UPDATE LEVEL NUMBERS                *|
    \*--------------------------------------------------*/
IF RELEVEL THEN DO L=1 TO ZLAST
  "ISREDIT (LINE) = LINE" L                /* GET CURRENT LINE */
  IF RC>0 THEN EXIT RC
  LEV=RIGHT(LINE,2) + 0                /* EXTRACT LEVEL             */
  IF LEV\=N.LEV THEN DO                /* LEVEL NEEDS BE CHANGED    */
    LINE2 = LEFT(LINE,LENGTH(LINE)-2) || RIGHT(N.LEV,2,'0')
    "ISREDIT LINE" L "= (LINE2)"       /* UPDATE CURRENT LINE */
    END
END

"ISREDIT (L) = LEVEL"                    /* CURRENT LEVEL NUMBER */
IF L \= N THEN
  "ISREDIT LEVEL =" N                    /* NEW LEVEL NUMBER    */
